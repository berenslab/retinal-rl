// Directives

#library "RETINAL"
#include "zcommon.acs"

// Variables

// Global Variables
// Reward
global int 0:reward;

// Frequency and amount of metabolic damage
global int 1:metabolic_delay;
global int 2:metabolic_damage;
global str 3:actor_names[];
global str 4:actor_num_textures[];

// Nourishment variables
global int 5:nourishment_unique;
global int 6:nourishment_init;
global int 7:nourishment_delay;

// Poison variables
global int 8:poison_unique;
global int 9:poison_init;
global int 10:poison_delay;

// Obstacle variables
global int 11:obstacle_unique;
global int 12:obstacle_init;
global int 13:obstacle_delay;

// Distractor variables
global int 14:distractor_unique;
global int 15:distractor_init;
global int 16:distractor_delay;

// Local variables

// Arena settings
int wall_min  = -1000.0;
int wall_max = 1000.0;

// Spawning actor by index
function void SpawnActor(int oid) {

    int tid = UniqueTID();
    str act = actor_names[oid];

    str texture_num = random(0,actor_num_textures[oid] - 1);
    str code = objectCode(oid,texture_num);
    str texture = StrParam(s:"Tex",s:code);
    // print(s:act);
    Spawn(act, random(wall_min+1.0,wall_max-1.0), random(wall_min+1.0,wall_max-1.0), 20.0,tid);
    SetActorState(tid, texture);
}

// Object Codes

str aalphas = "ABCDEGHIJKLMNPQRSTUVWXYZ";
str alphas = "ABCDEFGHIJKLMNPQRSTUVWXYZ";

function str textureCode(int j) {

    int lett_one = GetChar(alphas, j / (26*26) );
    int lett_two = GetChar(alphas,(j/26) % 26);
    int lett_thr = GetChar(alphas,j % 26);
    str code = StrParam(c:lett_one, c:lett_two, c:lett_thr);

    return code;
}

function str objectCode(int oid, int j) {

    str food_code = GetChar(aalphas,oid);
    str txt_code = textureCode(j);
    str code = strParam(c:food_code, s:txt_code);

    return code;
}

// Random object generation

function void randomNourishment(void) {
    int oid = random(0,nourishment_unique-1);
    SpawnActor(oid);
}

function void randomPoison(void) {
    int poison_offset = nourishment_unique;
    int oid = random(0,poison_unique-1);
    SpawnActor(oid+poison_offset);
}

function void randomObstacle(void) {
    int obstacle_offset = nourishment_unique + poison_unique;
    int oid = random(0,obstacle_unique-1);
    SpawnActor(oid+obstacle_offset);
}

function void randomDistractor(void) {
    int distractor_offset = nourishment_unique + poison_unique + obstacle_unique;
    int oid = random(0,distractor_unique-1);
    SpawnActor(oid+distractor_offset);
}

// Environment behaviour
script "Nourishment Spawning" ENTER {

    reward = 0;

    int i;

    for(i=0;i<nourishment_init;++i) { randomNourishment(); }

    while(nourishment_delay) {
        delay(nourishment_delay);
        randomNourishment();
    }
}

script "Poison Spawning" ENTER {

    int i;

    for(i=0;i<poison_init;++i) { randomPoison(); }

    while(poison_delay > 0) {
        delay(poison_delay);
        randomPoison();
    }
}

script "Obstacle Spawning" ENTER {

    int i;

    for(i=0;i<obstacle_init;++i) { randomObstacle(); }

    while(obstacle_delay > 0) {
        delay(obstacle_delay);
        randomObstacle();
    }
}

script "Distractor Spawning" ENTER {

    int i;

    for(i=0;i<distractor_init;++i) { randomDistractor(); }

    while(distractor_delay > 0) {
        delay(distractor_delay);
        randomDistractor();
    }
}

script "Agent Metabolism" ENTER {

    while(metabolic_delay > 0) {
        delay(metabolic_delay);
        Thing_Damage(0,metabolic_damage);
    }
}

script "Agent Initialization" ENTER {

    int act_x = random(wall_min,wall_max);
    int act_y = random(wall_min,wall_max);
    SetActorPosition(0, act_x, act_y, 0.0, 0);
    SetActorAngle(0,random(0.0,1.0));
    ClearInventory();

    int tcks = 0;

    while(true) {
        int curr_health = PlayerHealth();
        reward = reward + curr_health;
        delay(1);
    }
}

